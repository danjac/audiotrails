@_default:
    @just --list

# Run Ansible playbook
@pb playbook *ARGS:
    ansible-playbook -v -i hosts.yml playbooks/{{ playbook }}.yml {{ ARGS }}


# Set up the hosts
site:
    just pb site

# Set up the databases
database:
    just pb database

# Set up k3s on all hosts
k3s:
    just pb k3s

# Upgrade packages on all hosts
upgrade:
    just pb upgrade

# Run Django manage.py commands
dj *ARGS:
    #!/usr/bin/bash
    if [ ! -f "./manage.sh" ]; then
        echo "manage.sh not found, generating it..."
        just pb dj_manage
    fi
    ./manage.sh {{ ARGS }}

# Run Kubectl commands
kubectl *ARGS:
    #!/usr/bin/bash
    if [ ! -f "./kubectl.sh" ]; then
        echo "kubectl.sh not found, generating it..."
        just pb kubectl
    fi
    ./kubectl.sh {{ ARGS }}


# Run Psql commands
psql *ARGS:
    #!/usr/bin/bash
    if [ ! -f "./psql.sh" ]; then
        echo "pql.sh not found, generating it..."
        just pb psql
    fi
    ./psql.sh {{ ARGS }}
