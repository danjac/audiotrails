@_default:
    @just --list

# Setup the project with default settings
@setup:
    cp -R -u -p hosts.yml.example hosts.yml
    cp -R -u -p vars/settings.yml.example vars/settings.yml

# Run Ansible playbook
@playbook *ARGS:
    ansible-playbook -v -i hosts.yml {{ ARGS }}

# Deploy to all servers
@site:
    @just playbook site.yml

# Deploy cron scheduler and app servers
@deploy:
    @just playbook deploy.yml

# Upgrade all servers
@upgrade:
    @just playbook upgrade.yml

# Generate manage.sh script
@djmanage:
    @just playbook dj_manage.yml

# Generate manage.sh script
@psqlcreate:
    @just playbook psql.yml

# Run Django manage.py commands
dj *ARGS:
    #!/usr/bin/bash
    if [ ! -f "./manage.sh" ]; then
        echo "manage.sh not found, generating it..."
        just djmanage
    fi
    ./manage.sh {{ ARGS }}

# Run Psql commands
psql *ARGS:
    #!/usr/bin/bash
    if [ ! -f "./psql.sh" ]; then
        echo "pql.sh not found, generating it..."
        just psqlcreate
    fi
    ./psql.sh {{ ARGS }}
